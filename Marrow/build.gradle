plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.github.skeletonarmy.marrow'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_14
        targetCompatibility JavaVersion.VERSION_14
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    implementation 'com.acmerobotics.roadrunner:ftc:0.1.21'
    implementation 'com.acmerobotics.roadrunner:actions:1.0.1'
    implementation "dev.frozenmilk.dairy:CachingHardware:1.0.0"
}

def createSourcesJarTask() {
    tasks.register("sourcesJar", Jar) {
        from android.sourceSets.main.java.srcDirs
        archiveClassifier.set("sources")
    }
}

afterEvaluate {
    createSourcesJarTask()

    publishing {
        publications {
            create("release", MavenPublication) {
                def releaseComponent = components.findByName("release")
                if (releaseComponent != null) {
                    from releaseComponent
                }

                groupId = 'com.github.Skeleton-Army'
                artifactId = 'Marrow'
                version = 'main-SNAPSHOT'

                artifact tasks.named("sourcesJar")
            }
        }
    }
}
